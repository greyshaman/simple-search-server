@PACKAGE_INIT@

macro(import_targets type)
    if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/search-engine-${type}-targets.cmake")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "search-engine ${type} libraries were requested but not found"
        )
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND OFF)
        return()
    endif()

    include("${CMAKE_CURRENT_LIST_DIR}/search-engine-${type}-targets.cmake")
endmacro()

if(NOT TARGET search-server::SearchEngine)
    set(type "")

    if(DEFINED SEARCH_ENGINE_SHARED_LIBS)
        if(SEARCH_ENGINE_SHARED_LIBS)
            set(type "shared")
        else()
            set(type "static")
        endif()
    elseif(BUILD_SHARED_LIBS AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/search-engine-shared-targets.cmake")
        set(type "shared")
    elseif(EXISTS "${CMAKE_CURRENT_LIST_DIR}/search-engine-static-targets.cmake")
        set(type "static")
    else()
        set(type"shared")
    endif()

    import_targets(${type})
    check_required_components(search-engine)
    message("-- Found ${type} search-engine (version ${${CMAKE_FIND_PACKAGE_NAME}_VERSION})")
endif()
